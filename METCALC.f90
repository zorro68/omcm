!-----------------------------------------------------------------------
!    CALCULO ITERATIVO PARA ELECTRONES APAREADOS Y DESAPAREADOS
!-----------------------------------------------------------------------
	SUBROUTINE METCALC(FICHERO)
	USE GLOBAL
	USE ESTRUCT
	CHARACTER*(*) FICHERO
	INTEGER*4 I,MAT,NUMINT
	CHARACTER*ILONG LEERVALOR,CADIDIOMA,CAD
	
	IDIOMA=LEERVALOR('idioma.lng','Idioma','Idioma')
	
	CALL DEFINE_FICH(FICH.DAT,DIR.DAT,TRIM(FICHERO),'DAT')
	CALL DEFINE_FICH(FICH.MTD,DIR.MTD,TRIM(FICHERO),'MTD')
	CALL LEEDAT() !LECTURA DEL FICHEO DE DATOS
	CALL DEFINE_FICH(FICH.INT,DIR.INT,FICH.INT,'INT')
	CALL DEFINE_FICH(FICH.AUX,DIR.AUX,TRIM(FICHERO),'AUX')
	CALL LEETOQASINT()
	CALL DEFINE_FICH(FICH.SCR,DIR.SCR,TRIM(FICHERO),'SCR')
	
	CADIDIOMA=LEERVALOR('idioma.lng',IDIOMA,'1217')
	CAD=' '
	CALL MENSAJES(CADIDIOMA,CAD,100.Q0)
	
	CALL SCFHF(DI.ITMET,DI.CONV)
	
	END SUBROUTINE

! ------------------------------------------------------------------
! ---  CALCULO ITERATIVO PARA ELECTRONES APAREADOS Y DESAPAREADOS:
! ------------------------------------------------------------------
    SUBROUTINE SCFHF(ITMET,CONV)
    USE GLOBAL
	USE ESTRUCT
	IMPLICIT REAL*16(A-H,O-Z)

	REAL*16 NA(1:N,1:N),NB(1:N,1:N),AUX(1:N,1:N),HH(1:N,1:N),CMA(1:N,1:N),CMB(1:N,1:N)
	REAL*16 SS(1:N,1:N),SR(1:N,1:N),SFSA(1:N,1:N),SFSB(1:N,1:N),AUXA(1:N,1:N),AUXB(1:N,1:N) 
	REAL*16 EOR0A(1:N)
	REAL*16 FFA(1:N,1:N),FFB(1:N,1:N),F0A(1:N,1:N),F0B(1:N,1:N)
	INTEGER*4 ITMET,CONT,ERROR
	REAL*16 CONV
	LOGICAL CRICONV
	
	ALLOCATE (ENEL(0:DI.MAXITER),STAT=ERROR)
	IF (ERROR/=0) STOP 'NO HAY MEMORIA SUFICIENTE'
	FORALL(I=0:DI.MAXITER) ENEL(I)=0.Q0
	ALLOCATE (VIRIAL(0:DI.MAXITER),STAT=ERROR)
	IF (ERROR/=0) STOP 'NO HAY MEMORIA SUFICIENTE'
	FORALL(I=0:DI.MAXITER) VIRIAL(I)=0.Q0

!-------INICIALIZAMOS VARIABLES------
	FORALL(I=1:N,J=1:N) NA(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) NB(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) SR(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) SFSA(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) SFSB(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) AUXA(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) AUXB(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) SS(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) HH(I,J)=0.Q0
	FORALL(I=1:N) EORA(I)=0.Q0
	FORALL(I=1:N) EOR0A(I)=0.Q0
	FORALL(I=1:N) EORB(I)=0.Q0
	FORALL(I=1:N,J=1:N) FFA(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) FFB(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) F0A(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) F0B(I,J)=0.Q0
	
	CEROS=1.Q-10

	DO I=1,N
		DO J=1,N
			SS(I,J)=S(INPQ_F(I,J)) 
			HH(I,J)=H(INPQ_F(I,J)) 
		ENDDO
	ENDDO
		
! ---------- CALCULO DE LA POTENCIA -1/2 DE LA MATRIZ S: -------------
    CALL MATINVR(N,SS,SR)

! ---  CALCULO DE LA ENERGÍA DE REPULSION NUCLEAR:
	EREP=0.Q0
	DO K1=1,NUC
		DO K2=K1+1,NUC
			RK1K2=0.Q0
			RK1K2=RK1K2+(NUCLEOS(K1).X-NUCLEOS(K2).X)**2
			RK1K2=RK1K2+(NUCLEOS(K1).Y-NUCLEOS(K2).Y)**2
			RK1K2=RK1K2+(NUCLEOS(K1).Z-NUCLEOS(K2).Z)**2
			RK1K2=QSQRT(RK1K2)
			EREP=EREP+NUCLEOS(K1).CARGA*NUCLEOS(K2).CARGA/RK1K2		
		END DO
	END DO

	ENUC=EREP
    DO K=1,NUC
		ENUC=ENUC+NUCLEOS(K).CARGA*(NUCLEOS(K).X*POT(1)+NUCLEOS(K).Y*POT(2)+NUCLEOS(K).Z*POT(3))
	END DO
      
! -------------------- REALIZACION DE ITERACIONES: -------------------
    NIT=0
	CRICONV=.FALSE.
	
	!CONSTRUCCION DE LAS MATRICES DE NUMEROS DE OCUPACION
	FORALL(I=1:N) NA(I,I)=NOCA(I)
	FORALL(I=1:N) NB(I,I)=NOCB(I)
	!CONSTRUCCION DE LAS MATRICES DE COEFICIENTES INICIALES AFECTADAS POR EL NUMERO DE OCUPACIÓN
	CMA=MATMUL(NA,C0A)
	CMB=MATMUL(NB,C0B)
	!CALCULO DE LA MATRIZ DENSIDAD, ALFA Y BETA
	PA=MATMUL(TRANSPOSE(CMA),CMA)
	PB=MATMUL(TRANSPOSE(CMB),CMB)
	
	!CALCULO DE LA MATRIZ DE FOCK
	CALL CALFAB(PA,PB,F0A,F0B)

	!CALCULO DE LA ENERGÍA
	ENEL0=0.5Q0*(TRACE(N,MATMUL(PA,F0A+HH))+TRACE(N,MATMUL(PB,F0B+HH)))

	FFA=F0A
	FFB=F0B
	
	DO WHILE (CRICONV==.FALSE..AND.NIT<DI.MAXITER)
		NIT=NIT+1

		SFSA=MATMUL(SR,MATMUL(FFA,SR))
		CALL JACOBI(N,SFSA,AUXA,.TRUE.)

		DO I=1,N
			EORA(I)=SFSA(I,I)
		END DO

		IF (DI.THF=='UHF')THEN
			SFSB=MATMUL(SR,MATMUL(FFB,SR))
			CALL JACOBI(N,SFSB,AUXB,.TRUE.)
		
			DO I=1,N
				EORB(I)=SFSB(I,I)
			END DO
		ELSE
			EORB=EORA
		ENDIF

		! OBTENCION DE C 
		CA=MATMUL(AUXA,SR) !¿PORQUÉ, SI DEBE SER SR*AUXA?

		IF (DI.THF=='UHF')THEN
			CB=MATMUL(AUXB,SR)
		ELSE
			CB=CA		
		ENDIF	

		IF (NIT==1)THEN !SOLO PARA LA ITERACIÓN INICIAL
			!CONSTRUCCION DE LAS MATRICES DE COEFICIENTES INICIALES AFECTADAS POR EL NUMERO DE OCUPACIÓN
			CMA=MATMUL(NA,CA)
			CMB=MATMUL(NB,CB)
			!CALCULO DE LA MATRIZ DENSIDAD, ALFA Y BETA
			PA=MATMUL(TRANSPOSE(CMA),CMA)
			PB=MATMUL(TRANSPOSE(CMB),CMB)
	
			!CALCULO DE LA ENERGÍA
			ENEL0=0.5Q0*(TRACE(N,MATMUL(PA,F0A+HH))+TRACE(N,MATMUL(PB,F0B+HH)))
			
			CALL VALORESMEDIOS(ENEL0,ENUC,TMEDIA,VMEDIA,VIRIAL(0))
			ENEL(0)=ENEL0
		ENDIF
		
		IF(ITMET==2.AND.DI.F_CONV/=0.Q0.AND.NIT>=DI.NIT_CONV)THEN
			CA=(1.Q0-DI.F_CONV)*CA+DI.F_CONV*C0A
			CB=(1.Q0-DI.F_CONV)*CB+DI.F_CONV*C0B
		ENDIF
		
		!CONSTRUCCION DE LAS MATRICES DE COEFICIENTES INICIALES AFECTADAS POR EL NUMERO DE OCUPACIÓN
		CMA=MATMUL(NA,CA)
		CMB=MATMUL(NB,CB)
		!CALCULO DE LA MATRIZ DENSIDAD, ALFA Y BETA
		PA=MATMUL(TRANSPOSE(CMA),CMA)
		PB=MATMUL(TRANSPOSE(CMB),CMB)
		
		CALL CALFAB(PA,PB,FFA,FFB)
		
		IF(ITMET==1.AND.DI.F_CONV/=0.Q0.AND.NIT>=DI.NIT_CONV)THEN
			FFA=(1.Q0-DI.F_CONV)*FFA+DI.F_CONV*F0A
			FFB=(1.Q0-DI.F_CONV)*FFB+DI.F_CONV*F0B
		ENDIF

		!CALCULO DE LA ENERGÍA
		ENEL(NIT)=0.5Q0*(TRACE(N,MATMUL(PA,FFA+HH))+TRACE(N,MATMUL(PB,FFB+HH)))
		
		CALL VALORESMEDIOS(ENEL(NIT),ENUC,TMEDIA,VMEDIA,VIRIAL(NIT))
			
! ----- FIN DE UNA ITERACION
		F0A=FFA
		F0B=FFB

		!CADIDIOMA=LEERVALOR('idioma.lng',IDIOMA,'1220')
		!CADIDIOMA1=LEERVALOR('idioma.lng',IDIOMA,'1221')
		!WRITE(CAD,'(A,A1,I4,4X,A,A2,G17.10)')TRIM(CADIDIOMA),':',NIT,TRIM(CADIDIOMA1),' =',ENEL(NIT)
		!CAD=' '
		!CALL MENSAJES(CADIDIOMA,CAD,0.Q0)

! ---- CONTROL DE LA CONVERGENCIA POR ENERGIAS:
		IF(ITMET==1)THEN
			CRI=QABS(ENEL(NIT)-ENEL0)
			IF(CRI>CONV)THEN
				ENEL0=ENEL(NIT)
				FORALL(I=1:N,J=1:N)C0A(I,J)=CA(I,J)
				FORALL(I=1:N,J=1:N)C0B(I,J)=CB(I,J)
			ELSE
				CRICONV=.TRUE.
			ENDIF
		ENDIF

! ---- CONTROL DE LA CONVERGENCIA POR COEFICIENTES:
		IF(ITMET==2)THEN
			DO K=1,N
				DO IP=1,N
					IF(NOCA(K)==1)CRIA=QABS(CA(K,IP)-C0A(K,IP))
					IF(NOCB(K)==1)CRIB=QABS(CB(K,IP)-C0B(K,IP))
					IF(CRIA>CONV.OR.CRIB>CONV)CRICONV=.TRUE.
				END DO
			END DO
		ENDIF

		ERENEL=QABS(ENEL(NIT)-ENEL0)

	ENDDO

! ---  CALCULO DE ETOT=ENEL+REPNUC:
	ENELTOT=ENEL(NIT)
	ETOT=ENELTOT+ENUC

    IF (SCF=='MP2') THEN
		IF (DI.THF=='UHF')THEN
			CALL MP2OPN()
		ELSE
			CALL MP2CLO()
		ENDIF
	ENDIF

	CALL ESCSCF(NIT) !ESCRIBE LOS RESULTADOS 	
    END

! --------------------------------------------------------
! -------  CALCULO DE VALORES MEDIOS Y VIRIAL
! --------------------------------------------------------
	SUBROUTINE VALORESMEDIOS(ENER,ENUCL,TM,VM,VR)
		USE GLOBAL
		REAL*16 TM,VM,VR,UR,ENER,ENUCL,QA,QB,Q
		INTEGER*4 INPQ_F,IP,IQ,IN
		
		!TM=0.Q0
		!DO IP=1,N
		!	DO IQ=1,N
		!		QA=0.Q0
		!		QB=0.Q0
		!		DO IN=1,N
		!			IF(NOCA(IN)==1)QA=QA+CA(IN,IP)*CA(IN,IQ)
		!			IF(NOCB(IN)==1)QB=QB+CB(IN,IP)*CB(IN,IQ)
		!		END DO
		!		Q=QA+QB
		!		TM=TM+Q*T(INPQ_F(IP,IQ))
		!	END DO
		!END DO
		!VM=ENER-TM
		!VR=-VM/TM

		TM=0.Q0
		DO I=1,N
			DO J=1,N
				TM=TM+(PA(I,J)+PB(I,J))*T(INPQ_F(I,J))
			ENDDO
		ENDDO
		VM=ENER-TM
		!VR=-VM/TM !SIN REPULSION INTERNUCLEAR
		VR=(TM-ENER-ENUCL)/TM  !CON REPULSIÓN INTERNUCLEAR (APROX 2.000)
		RETURN
	END 

! --------------------------------------------------------
! -------  CALCULO DE F(p,q) PARA ORBITALES ALFA Y BETA: 
! --------------------------------------------------------
	SUBROUTINE CALFAB(PAA,PBB,FAA,FBB)
	USE GLOBAL
    IMPLICIT REAL*16(A-H,O-Z)

	REAL*16 PAA(1:N,1:N),PBB(1:N,1:N)
	REAL*16 FAA(1:N,1:N),FBB(1:N,1:N)

	DO INP=1,N
		DO INQ=1,N
			INDICE=INPQ_F(INP,INQ)
			FAA(INP,INQ)=H(INDICE)
			FBB(INP,INQ)=H(INDICE)

			DO INR=1,N
				DO INS=1,N
					PAB= PAA(INR,INS) + PBB(INR,INS)
					K1=INPQ_F(INP,INQ)
					K2=INPQ_F(INR,INS)
					K=INPQ_F(K1,K2)
					L1=INPQ_F(INP,INR)
					L2=INPQ_F(INQ,INS)
					L=INPQ_F(L1,L2)
					FAA(INP,INQ)= FAA(INP,INQ) + PAB*R(K) - PAA(INR,INS)*R(L)
					FBB(INP,INQ)= FBB(INP,INQ) + PAB*R(K) - PBB(INR,INS)*R(L)
				END DO
			END DO
		END DO
	END DO

	RETURN 
    END

! ---------------------------------------------------
! ---------- CALCULO MOLLER PLESSET DE 2º ORDEN -----
! ----------    CAPAS ABIERTAS (OPEN SHELL)     -----
! ---  SEGUN LA FORMULA 6.72 DEL SZABO Y OSTLUND. ---
! ---------------------------------------------------
	SUBROUTINE MP2OPN()
	USE GLOBAL
	REAL*16 JARBS,JASBR,EA,EB,ER,ES
	REAL*16 JIJKL,JIJKL_F,AIJKL,VAL
	INTEGER*4 IA,IB,IS,IR,NO,J
	INTEGER*4 ERROR
	REAL*16,ALLOCATABLE :: CFA(:)
	REAL*16,ALLOCATABLE :: CFB(:)
	REAL*16,ALLOCATABLE :: CFR(:)
	REAL*16,ALLOCATABLE :: CFS(:)
	CHARACTER*4 SA,SB,SR,SS

	ALLOCATE (CFA(1:N),STAT=ERROR)
	ALLOCATE (CFB(1:N),STAT=ERROR)
	ALLOCATE (CFR(1:N),STAT=ERROR)
	ALLOCATE (CFS(1:N),STAT=ERROR)	

	ECMP2=0.Q0
	j=0

	N2=2*N
	DO IB=1,N2
	IF(IB<=N)NO=NOCA(IB)
	IF(IB>N)NO=NOCB(IB-N)
	IF (NO==1)THEN
	  	DO IA=1,N2
		IF(IA<=N)NO=NOCA(IA)
		IF(IA>N)NO=NOCB(IA-N)
		IF (NO==1)THEN						
			DO IS=1,N2
			IF(IS<=N)NO=NOCA(IS)
			IF(IS>N)NO=NOCB(IS-N)
			IF (NO==0)THEN			
				DO IR=1,N2
				IF(IR<=N)NO=NOCA(IR)
				IF(IR>N)NO=NOCB(IR-N)
				IF (NO==0)THEN				    										
					IF(IB<=N)THEN
					  EB=EORA(IB)
					  SB='ALFA'
					  DO J=1,N
						CFB(J)=CA(IB,J)
					  ENDDO
					ENDIF
					IF(IB>N)THEN
					  EB=EORB(IB-N)
					  SB='BETA'
					  DO J=1,N
						CFB(J)=CB(IB-N,J)
					  ENDDO
					ENDIF

					IF(IA<=N)THEN
					  EA=EORA(IA)
					  SA='ALFA'
					  DO J=1,N
						CFA(J)=CA(IA,J)
					  ENDDO
					ENDIF
					IF(IA>N)THEN
					  EA=EORB(IA-N)
					  SA='BETA'
					  DO J=1,N
						CFA(J)=CB(IA-N,J)
					  ENDDO
					ENDIF

					IF(IS<=N)THEN
					  ES=EORA(IS)
					  SS='ALFA'
					  DO J=1,N
						CFS(J)=CA(IS,J)
					  ENDDO
					ENDIF
					IF(IS>N)THEN
					  ES=EORB(IS-N)
					  SS='BETA'
					  DO J=1,N
						CFS(J)=CB(IS-N,J)
					  ENDDO
					ENDIF

					IF(IR<=N)THEN
					  ER=EORA(IR)
					  SR='ALFA'
					  DO J=1,N
						CFR(J)=CA(IR,J)
					  ENDDO
					ENDIF
					IF(IR>N)THEN
					  ER=EORB(IR-N)
					  SR='BETA'
					  DO J=1,N
						CFR(J)=CB(IR-N,J)
					  ENDDO
					ENDIF					
					
					VAL=0.Q0
					IF(SA==SR.AND.SB==SS)VAL=VAL+JIJKL_F(CFA,CFB,CFR,CFS)
					IF(SA==SS.AND.SB==SR)VAL=VAL-JIJKL_F(CFA,CFB,CFS,CFR)

					ECMP2=ECMP2+VAL**2/(EA+EB-ER-ES)
				ENDIF
				ENDDO
			ENDIF
			ENDDO
		ENDIF
		ENDDO
	ENDIF
	ENDDO
	
	ECMP2=0.25Q0*ECMP2

	END 

! ---------------------------------------------------------
! ---------- CALCULO MOELLER PLESSET DE 2º ORDEN ----------
! ----------    CAPAS CERRADAS (CLOSED SHELL)    ----------
! ------  SEGUN LA FORMULA 6.74 DEL SZABO Y OSTLUND. ------
! ---------------------------------------------------------
	SUBROUTINE MP2CLO()
	USE GLOBAL
	REAL*16 EA,EB,ER,ES,ARBS,RSAB,RSBA
	REAL*16 JIJKL,JIJKL_F
	INTEGER*4 I,IA,IB,IS,IR
	INTEGER*4 ERROR
	REAL*16,ALLOCATABLE :: CFA(:)
	REAL*16,ALLOCATABLE :: CFB(:)
	REAL*16,ALLOCATABLE :: CFR(:)
	REAL*16,ALLOCATABLE :: CFS(:)

	ALLOCATE (CFA(1:N),STAT=ERROR)
	ALLOCATE (CFB(1:N),STAT=ERROR)
	ALLOCATE (CFR(1:N),STAT=ERROR)
	ALLOCATE (CFS(1:N),STAT=ERROR)
	

	ECMP2=0.Q0

	DO IB=1,N
		IF (NOCA(IB)==1)THEN
	  		DO IA=1,N 
				IF (NOCA(IA)==1)THEN						
					DO IS=1,N
						IF (NOCA(IS)==0)THEN		
							DO IR=1,N 
								IF (NOCA(IR)==0)THEN						
										DO I=1,N
										  CFB(I)=CA(IB,I)
										  CFA(I)=CA(IA,I)
										  CFS(I)=CA(IS,I)
										  CFR(I)=CA(IR,I)
										ENDDO
										ES=EORA(IS)
										ER=EORA(IR)	
										EB=EORA(IB)
										EA=EORA(IA)						
										ABRS=JIJKL_F(CFA,CFB,CFR,CFS)
										RSAB=JIJKL_F(CFR,CFS,CFA,CFB)				
										RSBA=JIJKL_F(CFR,CFS,CFB,CFA)

										ECMP2=ECMP2+ABRS*(2.Q0*RSAB-RSBA)/(EA+EB-ER-ES)
								ENDIF
							ENDDO
						ENDIF
					ENDDO
				ENDIF
			ENDDO
		ENDIF
	ENDDO

	END 


! ------------------------------------------------------------------------
! ---- CALCULO DE INTEGRALES DE REPULSION ENTRE ORBITALES MOLECULARES ----
! ----     (CON EL MISMO ESPIN, I.E. NO DETECTA CEROS POR ESPIN)      ----
! ----                  ** NOTACION QUIMICA **                        ----
! ------------------------------------------------------------------------
 	REAL*16 FUNCTION JIJKL(CI,CJ,CK,CL)
 	USE GLOBAL
 	INTEGER*4 IP,IQ,IR,IS
	INTEGER*4 M ! M es el indice de la integral (p,q|r,s) en R(M).
 	REAL*16 CI(1:N),CJ(1:N),CK(1:N),CL(1:N) ! Coeficientes de los OM i,j,k,l.
 	
 	JIJKL=0.Q0
 
 	DO IP=1,N
 		DO IQ=1,N
 			DO IR=1,N
 				DO IS=1,N
 					M=INDK(IP,IQ,IR,IS)
 					JIJKL=JIJKL+CI(IP)*CJ(IQ)*CK(IR)*CL(IS)*R(M)
 				ENDDO
 			ENDDO
 		ENDDO
 	ENDDO

 	END

! ------------------------------------------------------------------------
! ---- CALCULO DE INTEGRALES DE REPULSION ENTRE ORBITALES MOLECULARES ----
! ----     (CON EL MISMO ESPIN, I.E. NO DETECTA CEROS POR ESPIN)      ----
! ----                  ** NOTACION FISICA **                        ----
! ------------------------------------------------------------------------
	REAL*16 FUNCTION JIJKL_F(CI,CJ,CK,CL)
	USE GLOBAL
	INTEGER*4 IP,IQ,IR,IS
	INTEGER*4 M ! M es el indice de la integral (p,q|r,s) en R(M).
	REAL*16 CI(1:N),CJ(1:N),CK(1:N),CL(1:N) ! Coeficientes de los OM i,j,k,l.
	REAL*16 JIJKL

	JIJKL_F=0.Q0

	DO IP=1,N
		DO IQ=1,N
			DO IR=1,N
				DO IS=1,N
					M=INDK(IP,IQ,IR,IS)
					JIJKL_F=JIJKL_F + CI(IP)*CK(IQ)*CJ(IR)*CL(IS)*R(M)
				ENDDO
			ENDDO
		ENDDO
	ENDDO

	END

! -------------------------------------------------------------
! ---------- CALCULO DE S**(-1/2) SIENDO SS UNA NxN -----------
! -------------------------------------------------------------
	SUBROUTINE MATINVR(N,SS,SR)
    IMPLICIT REAL*16(A-H,O-Z)
	INTEGER*4 N
	REAL*16 SS(1:N,1:N),SR(1:N,1:N),TT(1:N,1:N),AUX(1:N,1:N)
	
	CEROS=1.Q-08

	FORALL(I=1:N,J=1:N) SR(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) TT(I,J)=0.Q0
	FORALL(I=1:N,J=1:N) AUX(I,J)=SS(I,J)
	
	CALL JACOBI(N,AUX,TT,.TRUE.)

    DO I=1,N
		IF(AUX(I,I)<CEROS)AUX(I,I)=CEROS*DFLOAT(I)
		AUX(I,I)=1.Q0/QSQRT(AUX(I,I))
	END DO

	SR=MATMUL(TRANSPOSE(TT),MATMUL(AUX,TT))

    RETURN
    END

! -------------------------------------------------------------
! ---------- CALCULO DE LA TRAZA DE UNA MATRIZ      -----------
! -------------------------------------------------------------
	REAL*16 FUNCTION TRACE(N,M)
	INTEGER*4 I,N
	REAL*16 M(1:N,1:N)
	
	TRACE=0.Q0
	DO I=1,N
		TRACE=TRACE+M(I,I)
	ENDDO
	RETURN
	END

! -----------------------------------------------------------------------
! --- SUBRUTINA PARA ESCRIBIR LOS RESULTADOS DEL CALCULO SCF
! -----------------------------------------------------------------------
	SUBROUTINE ESCSCF(NIVEL)
	USE GLOBAL
	USE ESTRUCT
	CHARACTER*ILONG INTTOSTR
	CHARACTER*ILONG REALTOSTR
	INTEGER*4 NIVEL
		     
	OPEN(1,FILE=DIR.MTD,STATUS='UNKNOWN')
	
	!GUARDA LOS DATOS GENERALES
	WRITE(1,'(A)')'[DATOS]'
	WRITE(1,'(A,A)')'Nucleos=',TRIM(INTTOSTR(NUC))
	WRITE(1,'(A,A)')'NumFB=',TRIM(INTTOSTR(N))
	WRITE(1,*)
	
	WRITE(1,'(A)')'[METODOCALCULO]'
	WRITE(1,'(A,A)')'Metodo=',TRIM(SCF)
	WRITE(1,'(A,A)')'ControlConvergencia=',TRIM(INTTOSTR(DI.ITMET))
	WRITE(1,'(A,A)')'Tolerancia=',TRIM(REALTOSTR(DI.CONV))
	WRITE(1,'(A,A)')'FactorAmortiguacion=',TRIM(REALTOSTR(DI.F_CONV))
	WRITE(1,'(A,A)')'FactorComienzoAmortiguacion=',TRIM(INTTOSTR(DI.NIT_CONV))
	WRITE(1,'(A,A)')'NumMaxIter=',TRIM(INTTOSTR(DI.MAXITER))
	WRITE(1,*)
	!CALL GUARDAVALOR(DIR.MTD,'ITERACIONES','NumIteraciones',INTTOSTR(NIVEL))
	
	WRITE(1,'(A)')'[ESTADOELECTRONICO]'
	DO I=1,N
		WRITE(1,'(A,A)')'NumOcupAlfa('//TRIM(INTTOSTR(I))//')=',TRIM(INTTOSTR(NOCA(I)))
	ENDDO
	DO I=1,N
		WRITE(1,'(A,A)')'NumOcupBeta('//TRIM(INTTOSTR(I))//')=',TRIM(INTTOSTR(NOCB(I)))
	ENDDO
	DO I=1,N
		DO J=1,N
			WRITE(1,'(A,A)')'CoefOcupAlfa('//TRIM(INTTOSTR(I))//','//TRIM(INTTOSTR(J))//')=',TRIM(REALTOSTR(CA(I,J)))
		ENDDO
	END DO
	DO I=1,N
		DO J=1,N
			WRITE(1,'(A,A)')'CoefOcupBeta('//TRIM(INTTOSTR(I))//','//TRIM(INTTOSTR(J))//')=',TRIM(REALTOSTR(CB(I,J)))
		ENDDO
	END DO
	DO I=1,N
		WRITE(1,'(A,A)')'EnergiaOrbitalAlfa('//TRIM(INTTOSTR(I))//')=',TRIM(REALTOSTR(EORA(I)))
	ENDDO
	DO I=1,N
		WRITE(1,'(A,A)')'EnergiaOrbitalBeta('//TRIM(INTTOSTR(I))//')=',TRIM(REALTOSTR(EORB(I)))
	ENDDO
	WRITE(1,*)

	!GUARDA LOS DATOS DE LAS ITERACIONES
	WRITE(1,'(A)')'[ITERACIONES]'
	WRITE(1,'(A,A)')'NumIteraciones=',TRIM(INTTOSTR(NIVEL))
	WRITE(1,'(A,A)')'Tmedia=',TRIM(REALTOSTR(TMEDIA))
	WRITE(1,'(A,A)')'Vmedia=',TRIM(REALTOSTR(VMEDIA))
	DO I=0,NIVEL
		WRITE(1,'(A,A)')'Energia('//TRIM(INTTOSTR(I))//')=',TRIM(REALTOSTR(ENEL(I)))
		WRITE(1,'(A,A)')'Virial('//TRIM(INTTOSTR(I))//')=',TRIM(REALTOSTR(VIRIAL(I)))
	ENDDO
	WRITE(1,*)
	
	!GUARDA LOS DATOS DE LAS ENERGIAS
	WRITE(1,'(A)')'[ENERGIAS]'
	WRITE(1,'(A,A)')'EnerElectr=',TRIM(REALTOSTR(ENELTOT))
	WRITE(1,'(A,A)')'EnerRep=',TRIM(REALTOSTR(EREP))
	WRITE(1,'(A,A)')'EnerTotal=',TRIM(REALTOSTR(ETOT))
	WRITE(1,'(A,A)')'EnerMP2=',TRIM(REALTOSTR(ECMP2))
	WRITE(1,*)

	CLOSE(1)
	RETURN
	
	END

